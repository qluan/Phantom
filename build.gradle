/*
 * Copyright (C) 2017-2018 Manbang Group
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
//        mavenLocal()
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'

        classpath 'com.wlqq.phantom:phantom-host-gradle:3.0.0'
        classpath 'com.wlqq.phantom:phantom-plugin-gradle:3.0.0'

        classpath 'com.novoda:bintray-release:0.4.0'    // 0.4.0 以上的版本不支持目前使用的 Gradle 3.3
    }
}

allprojects {
    repositories {
//        mavenLocal()
        jcenter()
        maven {
            url 'https://maven.google.com'
        }
    }

    tasks.withType(Javadoc) {
        options {
            encoding 'UTF-8'
            charSet 'UTF-8'
            links 'http://docs.oracle.com/javase/7/docs/api'
        }
    }
}

ext {
    // android build tools version
    COMPILE_SDK_VERSION = 28
    BUILD_TOOLS_VERSION = '28.0.3'
    MIN_SDK_VERSION = 14
    TARGET_SDK_VERSION = 22

    // gradle maven dependencies version
    ANDROID_SUPPORT_LIB_VERSION = '28.0.0'
    ANDROID_MULTIDEX_VERSION = '1.0.3'
    JUNIT_VERSION = '4.12'
    ANDROID_TEST_SUPPORT_VERSION = '0.5'
    ESPRESSO_VERSION = '2.2.2'
    UIAUTOMATOR_VERSION = '2.1.2'

    // phantom version
    PHANTOM_VERSION_NAME = '3.0.0'
    PHANTOM_VERSION_CODE = 30000
}

subprojects { subProject ->
    subProject.tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion COMPILE_SDK_VERSION
                buildToolsVersion BUILD_TOOLS_VERSION

                defaultConfig {
                    minSdkVersion MIN_SDK_VERSION
                    targetSdkVersion TARGET_SDK_VERSION
                }

                lintOptions {
                    checkReleaseBuilds false
                    abortOnError false
                }

                packagingOptions {
                    exclude 'META-INF/LGPL2.1'
                    exclude 'META-INF/LICENSE'
                    exclude 'META-INF/LICENSE.txt'
                    exclude 'META-INF/NOTICE'
                    exclude 'META-INF/NOTICE.txt'
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_7
                    targetCompatibility JavaVersion.VERSION_1_7
                }
            }
        }
    }
}
