/*
 * Copyright (C) 2017-2018 Manbang Group
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.wlqq.phantom.plugin'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.wlqq.phantom.plugin.component"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 10000
        versionName "1.0.0"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFile 'proguard-phantom.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFile 'proguard-phantom.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(output.outputFile.parent,
                    "${variant.applicationId}_${variant.versionName}.apk")

            if (output.zipAlign != null) {
                output.zipAlign.doLast {
                    output.zipAlign.inputFile.delete()
                }
            }
        }
    }
}

dependencies {
    provided 'com.wlqq.phantom:phantom-communication-lib:3.0.0'
    provided 'com.wlqq.phantom:phantom-plugin-lib:3.0.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:support-v4:${ANDROID_SUPPORT_LIB_VERSION}"
    compile 'io.github.shaobin0604:awesomelog:1.0.1'

    testCompile 'junit:junit:4.12'
}

def deletePluginInHost() {
    println "deletePluginInHost..."
    def assetsDir = file(project(':phantom-sample:host').getProjectDir().absolutePath + '/src/main/assets/plugins')
    if (!assetsDir.exists()) {
        assetsDir.mkdirs()
    }
    assetsDir.eachFile { file ->
        println "list assets file: " + file.name
        if (file.name.startsWith(android.defaultConfig.applicationId)) {
            def ret = file.delete()
            println "delete assets file: ${file.name}, result: ${ret}"
        }
    }
}

def copyPluginToHost() {
    println "copy build plugin to app assets..."
    copy {
        from(buildDir.absolutePath + '/outputs/apk/') {
            include('*.apk')
        }

        into(project(':phantom-sample:host').getProjectDir().absolutePath + '/src/main/assets/plugins/')
    }
}

afterEvaluate { project ->
    project.tasks.assembleDebug.doLast {
        deletePluginInHost()
        copyPluginToHost()
    }

    project.tasks.assembleRelease.doLast {
        deletePluginInHost()
        copyPluginToHost()
    }
}

phantomPluginConfig {
    /*------------------------------ 剔除公共库配置 BEGIN ----------------------------*/
    excludeLib "com.android.support:support-v4:${ANDROID_SUPPORT_LIB_VERSION}"
    /*------------------------------ 剔除公共库配置  END  ----------------------------*/

    /*------------------------------ 混淆配置 BEGIN ------------------------------*/
    libraryJarsProguardFile file('proguard-phantom.pro')
    /*------------------------------ 混淆配置  END  ------------------------------*/

    /*------------------------------ 快速安装插件配置 BEGIN ------------------------*/
    // 宿主包名
    hostApplicationId = "com.wlqq.phantom.sample"
    // 宿主 launcher Activity full class name
    hostAppLauncherActivity = "com.wlqq.phantom.sample.MainActivity"

    // 插件包名
    pluginApplicationId = android.defaultConfig.applicationId
    // 插件版本名
    pluginVersionName = android.defaultConfig.versionName
    /*------------------------------ 快速安装插件配置  END  -------------------------*/
}
